/* Program */
program  -> basic id ( ) block
block    -> { decls stmts }

/* Declaration */
decls    -> decls decl | <epsilon>
decl     -> type id ;
type     -> type [ num ] | basic
basic    -> float | int | double

/* Statement */
stmts    -> stmts stmt | <epsilon>
stmt     -> id = expr ;
        | loc = expr ;
        | if ( bool ) stmt
        | if ( bool ) stmt else stmt
        | while ( bool ) stmt
        | do stmt while ( bool ) ;
        | break ;
        | block
loc      -> loc [ num ] 

/* Condition Expressions */
bool     -> bool || join | join
join     -> join && equality | equality
equality -> equality == rel  | equality != rel | rel
rel      -> expr < expr | expr <= expr | expr >= expr | expr > expr | expr

/* Arithmetic Expressions */
expr     -> expr + term | expr - term | term
term     -> term * unary | term / unary | unary
unary    -> ! unary | - unary | factor
factor   -> ( expr ) | loc | id | num | real | true | false